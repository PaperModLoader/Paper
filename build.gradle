buildscript {
    repositories {
        maven {
            name = "ilexiconn"
            url = "http://maven.ilexiconn.net/"
        }
    }
    dependencies {
        classpath "xyz.papermodloader:tree:0.1.0.+"
    }
}

plugins {
    id "java"
    id "maven-publish"
}

apply plugin: "xyz.papermodloader.tree"

group = "xyz.papermodloader"
version = getVersion()
archivesBaseName = "paper"
sourceCompatibility = targetCompatibility = "1.8"

if (System.getenv().BUILD_NUMBER) {
    version = version + "." + "${System.getenv().BUILD_NUMBER}"
}

repositories {
    mavenCentral()
}

dependencies {
    compile "com.google.code.gson:gson:2.7"
    compile "commons-io:commons-io:2.5"
    compile "org.ow2.asm:asm-debug-all:5.1"
}

paper {
    minecraft = "1.10.2"
    mappings = 5
}

String getVersion() {
    String version = "0.0.0"
    String prefix = "public static final String VERSION = \"";
    File file = file("src/main/java/xyz/papermodloader/paper/Paper.java")
    file.eachLine { String s ->
        s = s.trim();
        if (s.startsWith(prefix)) {
            s = s.substring(prefix.length(), s.length() - 2);
            version = s;
        }
    }
    return version;
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId "xyz.papermodloader"
            artifactId "paper"
            from components.java
        }
    }
    repositories {
        maven {
            credentials {
                if (System.getenv().PASSWORD) {
                    username System.getenv().USERNAME
                    password System.getenv().PASSWORD
                } else {
                    username ""
                    password ""
                }
            }
            if (System.getenv().PASSWORD) {
                url System.getenv().REPO
            } else {
                url "file:///var/www/maven/"
            }
        }
    }
}

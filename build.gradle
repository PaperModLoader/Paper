buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "ilexiconn"
            url = "http://maven.ilexiconn.net"
        }
    }
    dependencies {
        classpath "xyz.papermodloader:tree:0.1.0.+"
    }
}

plugins {
    id "java"
    id "maven-publish"
}

apply plugin: "xyz.papermodloader.tree.paper"

group = "xyz.papermodloader"
version = getVersion()
archivesBaseName = "paper"
sourceCompatibility = targetCompatibility = "1.8"

if (System.getenv().BUILD_NUMBER) {
    version = version + "." + "${System.getenv().BUILD_NUMBER}"
}

repositories {
    mavenCentral()
    maven {
        name = "sponge"
        url = "http://repo.spongepowered.org/maven"
    }
    maven {
        name = "ilexiconn"
        url = "http://maven.ilexiconn.net"
    }
}

dependencies {
    compile("org.spongepowered:mixin:0.5.11-SNAPSHOT") {
        exclude module: "launchwrapper"
        exclude module: "guava"
    }
    compile "xyz.papermodloader:book:0.1.0.17"
}

paper {
    minecraft = "1.10.2"
    mappings = 31
}

String getVersion() {
    String version = "0.0.0"
    String prefix = "public static final Version VERSION = new Version(\"";
    File file = file("src/main/java/xyz/papermodloader/paper/Paper.java")
    file.eachLine { String s ->
        s = s.trim();
        if (s.startsWith(prefix)) {
            s = s.substring(prefix.length(), s.length() - 3);
            version = s;
        }
    }
    return version + "-1.10.2";
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId "xyz.papermodloader"
            artifactId "paper"
            from components.java
        }
    }
    repositories {
        maven {
            credentials {
                if (System.getenv().PASSWORD) {
                    username System.getenv().USERNAME
                    password System.getenv().PASSWORD
                } else {
                    username ""
                    password ""
                }
            }
            if (System.getenv().PASSWORD) {
                url "sftp://connect.ilexiconn.net:22/home/ilexiconn/maven/"
            } else {
                url "file:///var/www/maven/"
            }
        }
    }
}
